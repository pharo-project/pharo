"
A changes browser for the changes generated by the rewrite rule editor
"
Class {
	#name : 'StRewriteRuleChangesBrowserPresenter',
	#superclass : 'SpPresenter',
	#instVars : [
		'#changes',
		'#selectedChanges',
		'#changeTree => ObservableSlot',
		'#diffView => ObservableSlot',
		'#buttonCancel => ObservableSlot',
		'#buttonOk => ObservableSlot'
	],
	#category : 'NautilusRefactoring-Utilities',
	#package : 'NautilusRefactoring',
	#tag : 'Utilities'
}

{ #category : 'instance creation' }
StRewriteRuleChangesBrowserPresenter class >> changes: aCollection [

	^ self new
		changes: aCollection;
		yourself
]

{ #category : 'layout' }
StRewriteRuleChangesBrowserPresenter class >> defaultLayout [

	^ SpBoxLayout newTopToBottom
		add: (SpPanedLayout newTopToBottom
			add: #changeTree;
			add: #diffView;
		yourself);
		add: (SpBoxLayout newLeftToRight
			addLast: #buttonCancel expand: false fill: true padding: 2;
			addLast: #buttonOk expand: false fill: false padding: 0;
			yourself)
		expand: false
		fill: false
		padding: 2;
		yourself
]

{ #category : 'testing' }
StRewriteRuleChangesBrowserPresenter class >> isDeprecated [ 

	^ true
]

{ #category : 'actions' }
StRewriteRuleChangesBrowserPresenter >> accept [
	self okToChange
		ifFalse: [ ^ self ].
	self selectedChanges isEmptyOrNil ifTrue: [ ^self ].
	[ self selectedChanges do: [ :change | RBRefactoryChangeManager instance performChange: change ] ] asJob
		title: 'Refactoring';
		run.
	self window delete
]

{ #category : 'update' }
StRewriteRuleChangesBrowserPresenter >> buildDiffFor: aChange [

	^ aChange
		ifNil: [ '' ]
		ifNotNil: [
			diffView
				contextClass: aChange changeClass;
				leftText: aChange oldVersionTextToDisplay;
				rightText: aChange textToDisplay ]
]

{ #category : 'accessing' }
StRewriteRuleChangesBrowserPresenter >> buttonCancel [
	^ buttonCancel
]

{ #category : 'accessing' }
StRewriteRuleChangesBrowserPresenter >> buttonCancel: anObject [
	buttonCancel := anObject
]

{ #category : 'accessing' }
StRewriteRuleChangesBrowserPresenter >> buttonOk [
	^ buttonOk
]

{ #category : 'accessing' }
StRewriteRuleChangesBrowserPresenter >> buttonOk: anObject [
	buttonOk := anObject
]

{ #category : 'actions' }
StRewriteRuleChangesBrowserPresenter >> cancel [
	self window delete
]

{ #category : 'accessing' }
StRewriteRuleChangesBrowserPresenter >> changeTree [
	^ changeTree
]

{ #category : 'accessing' }
StRewriteRuleChangesBrowserPresenter >> changeTree: anObject [
	changeTree := anObject
]

{ #category : 'accessing' }
StRewriteRuleChangesBrowserPresenter >> changes [
	^ changes
]

{ #category : 'accessing' }
StRewriteRuleChangesBrowserPresenter >> changes: anObject [
	changes := anObject.
	selectedChanges := OrderedCollection withAll: changes first changes.
	self updateChanges
]

{ #category : 'initialization' }
StRewriteRuleChangesBrowserPresenter >> connectPresenters [
	changeTree children: [ :aChange |
		(aChange isNotNil and: [aChange changesSize > 1])
			ifTrue: [ aChange changes ]
			ifFalse: [#()]].
	buttonCancel action: [ self cancel ].
	buttonOk action: [ self accept ]
]

{ #category : 'accessing' }
StRewriteRuleChangesBrowserPresenter >> diffView [
	^ diffView
]

{ #category : 'accessing' }
StRewriteRuleChangesBrowserPresenter >> diffView: anObject [
	diffView := anObject
]

{ #category : 'initialization' }
StRewriteRuleChangesBrowserPresenter >> initialize [
	changes := #().
	super initialize
]

{ #category : 'initialization' }
StRewriteRuleChangesBrowserPresenter >> initializePresenters [
	changeTree := self newTreeTable.
	changeTree addColumn: (
		SpCompositeTableColumn new
			title: 'Changes';
			addColumn: ((SpCheckBoxTableColumn title: ' ' evaluated: [ :change | selectedChanges includes: change ])
				onActivation: [ :change |
					selectedChanges add: change];
				onDeactivation: [ :change |
					selectedChanges remove: change];
				width: 20;
				yourself);
			addColumn: (SpStringTableColumn evaluated: #nameToDisplay));
		whenActivatedDo: [ :selection | self buildDiffFor: selection selectedItem  ].
	diffView := self newDiff.
	diffView showOptions: true.
	buttonCancel := self newButton label: 'Cancel'.
	buttonOk := self newButton label: 'Ok'.
	self focusOrder
		add: buttonCancel;
		add: buttonOk
]

{ #category : 'initialization' }
StRewriteRuleChangesBrowserPresenter >> initializeWindow: aWindowPresenter [
	| fontWidth rowHeight |
	fontWidth := (StandardFonts defaultFont widthOfString: 'M').
	rowHeight := self class inputTextHeight.
	aWindowPresenter
		title: self windowTitle;
		initialExtent: (70@20) * (fontWidth @ rowHeight)
]

{ #category : 'accessing' }
StRewriteRuleChangesBrowserPresenter >> selectedChanges [
	^ selectedChanges
]

{ #category : 'accessing' }
StRewriteRuleChangesBrowserPresenter >> selectedChanges: anObject [
	selectedChanges := anObject
]

{ #category : 'update' }
StRewriteRuleChangesBrowserPresenter >> updateChanges [
	| aCompositeChange |
	aCompositeChange := RBRefactoryChangeManager changeFactory
		compositeRefactoryChange.
	changes do: [ :each | aCompositeChange addChange: each ].

	"Later we could filter the shown changes depending on the selected scope"

	changeTree roots: (aCompositeChange whatToDisplayIn: self)
]

{ #category : 'initialization' }
StRewriteRuleChangesBrowserPresenter >> windowTitle [

	^ 'Changes Browser'
]
